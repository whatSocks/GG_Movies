= Movies GraphGist
:neo4j-version: 2.1.0  <-- what version
:author: Cristina
:description: The GraphGist of the Movies Database

== Table of Contents

[[introduction]]
== LOADCSV and the Neo Technology Movie Graph

This GraphGist demonstrates how to use the LOADCSV command in Cypher to build part of the iconic Neo Technology Movie Database example, and explore some of the queries used in the [name of movies webapp] example.  

Although we can put a lot of information and really build out relationships in the the Movie Database example, this Graph Gist will only focus on Movies, Persons, and the `(:Person)-[:ACTED_IN]->(:Movie)` relationship. 

== Using Load CSV

To import data from a CSV file into Neo4j, you can use LOAD CSV to get the data into your query, then you write it to your database using the normal updating clauses of Cypher.


Let's put in the Movies nodes from `movies.csv`, the contents of which looks something like this:

```
title|id|released|tagline|summary|rated|duration
21 Jump Street|62|2012|They thought the streets were mean. Then they went back to high school.|In high school,...|R|109"
A Few Good Men|5|1992|In the heart of the nation's capital...|Neo military lawyer Kaffee...|R|138
```

There are seven pipe-delimited fields, many rows, and a header row. Let's put them in. Note the double-quotes around the link to the CSV file.

[source,cypher]
----
LOAD CSV
WITH HEADERS FROM "https://raw.githubusercontent.com/whatSocks/GG_Movies/master/movies.csv" AS line FIELDTERMINATOR '|'
CREATE (:Movie { title: str(line.title), id: toInt(line.id), released: toInt(line.released), tagline: str(line.tagline), summary: str(line.summary), rated: str(line.rated), duration: line.duration})
----

Let's input the Person nodes. This is what they look like in the csv file:

```
name|id|born
Aaron Sorkin|24|1961
Adelaide Clemens|398|1989
```

Let's put them in.

[source,cypher]
----
LOAD CSV 
WITH HEADERS FROM "https://raw.githubusercontent.com/whatSocks/GG_Movies/master/persons.csv" AS line FIELDTERMINATOR '|' 
CREATE (:Person { name: str(line.name), id: toInt(line.id), born: toInt(line.born) })
----

Now Let's input the relationships between Persons and Movies. This is what they look like in the csv file. Again, there's a header file and the ids from the Person and Movie files. 


```
Person_id|Movie_id
437|62
```

here's the query:
[source,cypher]
----
LOAD CSV
WITH HEADERS
FROM "https://raw.githubusercontent.com/whatSocks/GG_Movies/master/acted_in.csv" AS line FIELDTERMINATOR '|'
MATCH (p:Person {id: toInt(line.Person_id)}), (m:Movie {id: toInt(line.Movie_id)})
MERGE (p)-[:ACTED_IN]-(m)
----

Let's take a look at what we got:

[source,cypher]
----
MATCH (a)-[r]->(b)
WHERE labels(a) <> [] AND labels(b) <> []
RETURN DISTINCT head(labels(a)) AS This, type(r) as To, head(labels(b)) AS That
----
//table

Although we were able to use out-of-the-box LOAD CSV in this example, if the CSV file contains a significant number of rows (approaching hundreds of thousands or millions), we should use `USING PERIODIC COMMIT` or Michael's batch-import. `USING PERIODIC COMMIT` instructs Neo4j to perform a commit after a number of rows. This reduces the memory overhead of the transaction state. By default, the commit will happen every 1000 rows. For more information, see Section 11.9, “Using Periodic Commit”.

http://docs.neo4j.org/chunked/stable/query-load-csv.html#load-csv-importing-large-amounts-of-data

== Building a Movie Recommendation Engine: Getting Started

Now that the data is loaded, we can have some fun. Even though there are only two node types and one relationship type, we can build and test a functional recommendation engine. 

First, let's define what we're recommending. If I really enjoy 70s-style acting and hate the thought of digital cinematography, but have already watched every movie between 1970 and 1989, I might be interested in the following query:

[source,cypher]
----
MATCH (actor:Person)-[:ACTED_IN]->(seventies:Movie),(actor)-[:ACTED_IN]->(new:Movie)
WHERE seventies.released > 1969 AND seventies.released < 1980 
AND new.released > 1979 AND new.released < 1999 //The Phantom Menace (1999) heralded the shift from film to digital
RETURN DISTINCT new.title AS `Beyond the 70s`
----
//table

This query just scratches the surface. On a real database you might want to attempt the challenges posed by the http://www.netflixprize.com/[Netflix Prize], build a recommendation machine for movies not usually found on Netflix (and there are a lot of them), or add more data and explore. 

== The Movies Web App

Neo Technology has built a web app to explore the movie database. link:http://neo4jmovies.herokuapp.com/#/movies[Take a look]!

The website uses link:http://movieapi-neo4j.herokuapp.com/docs/[Swagger], a language-agnostic framework implementation for describing, producing, consuming, and visualzing RESTful web services. The the Neo4j Movies app uses the link:https://github.com/tinj/node-neo4j-swagger-api[node-neo4j-swagger-api], which is particularly useful as it displays the Cypher associated with each call to an endpoint. 

For example, let's see what happens when we look for co-actors of, say, link:http://neo4jmovies.herokuapp.com/index.html#/people/Tom%20Hanks[Tom Hanks]. 

If a user were to meander over to the Tom Hank's Person page, they'd see a row of related actors at the bottom. These are Tom Hank's co-Actors, and are populated by the following query:

[source,cypher]
----
MATCH (actor:Person {name: "Tom Hanks"})
MATCH (actor)-[:ACTED_IN]->(m)
WITH m, actor
MATCH (m)<-[:ACTED_IN]-(person:Person)
WHERE actor <> person
RETURN person
----
//table

or, in a more graphical form:

[source,cypher]
----
MATCH p = (actor:Person {name: "Tom Hanks"})-[:ACTED_IN]->(m:Movie)<-[:ACTED_IN]-(person:Person)
WHERE actor <> person
RETURN p
----
//graph_result

What about the movies Tom Hanks has acted in? The web app sends this query:

[source,cypher]
----
MATCH (actor:Person {name: "Tom Hanks" })
MATCH (actor)-[:ACTED_IN]->(movie)
RETURN movie
----
//table

or, in a more graphical form:

[source,cypher]
----
MATCH p = (actor:Person {name: "Tom Hanks" })-[:ACTED_IN]->(movie:Movie)
RETURN p
----
//graph_result


== Conclusion


== References

- http://www.neo4j.org/learn/cypher[Learn Cypher]
- http://docs.neo4j.org/chunked/stable/cypherdoc-movie-database.html[The Movie Database]
- http://jexp.de/blog/2014/06/using-load-csv-to-import-git-history-into-neo4j/[Using LOAD CSV]


